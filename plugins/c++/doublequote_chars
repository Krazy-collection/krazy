#!/usr/bin/perl -w

eval 'exec /usr/bin/perl -w -S $0 ${1+"$@"}'
    if 0; # not running under some shell
###############################################################################
# Sanity check plugin for the Krazy project.                                  #
# Copyright (C) 2006-2008 by Allen Winter <winter@kde.org>                    #
#                                                                             #
# This program is free software; you can redistribute it and/or modify        #
# it under the terms of the GNU General Public License as published by        #
# the Free Software Foundation; either version 2 of the License, or           #
# (at your option) any later version.                                         #
#                                                                             #
# This program is distributed in the hope that it will be useful,             #
# but WITHOUT ANY WARRANTY; without even the implied warranty of              #
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the                #
# GNU General Public License for more details.                                #
#                                                                             #
# You should have received a copy of the GNU General Public License along     #
# with this program; if not, write to the Free Software Foundation, Inc.,     #
# 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.               #
#                                                                             #
###############################################################################

# Tests KDE source for adding a single char string to a QString, or other
# cases where QString containing a single char is better as a QChar.
# For example: QString str += "/"; should be QString += '/';

# Program options:
#   --help:          print one-line help message and exit
#   --version:       print one-line version information and exit
#   --explain:       print an explanation with solving instructions, then exit
#   --installed      file is to be installed
#   --quiet:         suppress all output messages
#   --verbose:       print the offending content

# Exits with status=0 if test condition is not present in the source;
# else exits with the number of failures encountered.

use strict;
use Getopt::Long;
use FindBin qw($Bin);
use lib "$Bin/../../../../lib";
use Krazy::PreProcess;

my($Prog) = "doublequote_chars";
my($Version) = "1.7";

my($help) = '';
my($version) = '';
my($explain) = '';
my($installed) = '';
my($quiet) = '';
my($verbose) = '';

exit 1
if (!GetOptions('help' => \$help, 'version' => \$version,
                'explain' => \$explain, 'installed' => \$installed,
                'verbose' => \$verbose, 'quiet' => \$quiet));

&Help() if $help;
&Version() if $version;
&Explain() if $explain;
if ($#ARGV != 0){ &Help(); exit 0; }

my($f) = $ARGV[0];

# open file and slurp it in (C++, non-headers only)
if ($f =~ m/\.cpp$/ || $f =~ m/\.cc$/ || $f =~ m/\.cxx$/) {
  open(F, "$f") || die "Couldn't open $f";
} else {
  print "okay\n" if (!$quiet);
  exit 0;
}
my(@data_lines) = <F>;
close(F);

# Remove C-style comments and #if 0 blocks from the file input
my(@lines) = RemoveIfZeroBlockC( RemoveCommentsC( @data_lines ) );

my($cnt) = 0;
my($scnt) = 0;
my($linecnt) = 0;
my($line);
my($lstr) = "";
my($slstr) = "";
foreach $line (@lines) {
  if ($line =~ m+//.*[Kk]razy:excludeall=.*$Prog+ ||
      $line =~ m+//.*[Kk]razy:skip+) {
    $cnt = 0;
    last;
  }

  $linecnt++;
  next if ($line =~ m+//.*[Kk]razy:exclude=.*$Prog+);
  $line =~ s+//.*++;  #skip C++ comments

  if ($line =~ m/\+[[:space:]]*\"[[:print:]]\"/ ||
      $line =~ m/\"[[:print:]]\"[[:space:]]*\+/ ||
      $line =~ m/\+=[[:space:]]*\"[[:print:]]\"/) {
    next if ($line =~ m/\\\"[[:print:]]\"/);
    next if ($line =~ m/\"[[:print:]]\\\"/);
    next if ($line =~ m/\"\+\",\"/ || $line =~ m/\",\"\+\"/); # "+","x
    next if ($line =~ m/\"\\\"/);
    next if ($line =~ m/\"[[:print:]]\"\s[A-Z]/);
    $cnt++;
    if ($cnt == 1) {
      $lstr = "line\#" . $linecnt;
    } else {
      $lstr = $lstr . "," . $linecnt;
    }
    print "=> $line\n" if ($verbose);
    next;
  }
  if ($line =~ m/\+[[:space:]]*\"\\n\"/ ||
      $line =~ m/\"\\n\"[[:space:]]*\+/ ||
      $line =~ m/\+=[[:space:]]*\"\\n\"/) {
    next if ($line =~ m/\\\"\\n\"/);
    next if ($line =~ m/\"\\n\\\"/);
    next if ($line =~ m/\"\\\"/);
    $cnt++;
    if ($cnt == 1) {
      $lstr = "line\#" . $linecnt;
    } else {
      $lstr = $lstr . "," . $linecnt;
    }
    print "=> $line\n" if ($verbose);
    next;
  }
  if ($line =~ m/\+[[:space:]]*\"\\t\"/ ||
      $line =~ m/\"\\t\"[[:space:]]*\+/ ||
      $line =~ m/\+=[[:space:]]*\"\\t\"/) {
    next if ($line =~ m/\\\"\\t\"/);
    next if ($line =~ m/\"\\t\\\"/);
    next if ($line =~ m/\"\\\"/);
    $cnt++;
    if ($cnt == 1) {
      $lstr = "line\#" . $linecnt;
    } else {
      $lstr = $lstr . "," . $linecnt;
    }
    print "=> $line\n" if ($verbose);
    next;
  }
  my ($pline) = $line;
  $pline =~ s/\\\"/ /g; # remove quoted stuff
  if ($pline =~ m/startsWith[[:space:]]*\([[:space:]]*\"[[:print:]]\"/ ||
      $pline =~ m/startsWith[[:space:]]*\([[:space:]]*\"\\n\"/ ||
      $pline =~ m/startsWith[[:space:]]*\([[:space:]]*\"\\t\"/ ||
      $pline =~ m/startsWith[[:space:]]*\([[:space:]]*\"\\\"\"/ ||
      $pline =~ m/endsWith[[:space:]]*\([[:space:]]*\"[[:print:]]\"/ ||
      $pline =~ m/endsWith[[:space:]]*\([[:space:]]*\"\\n\"/ ||
      $pline =~ m/endsWith[[:space:]]*\([[:space:]]*\"\\t\"/ ||
      $pline =~ m/endsWith[[:space:]]*\([[:space:]]*\"\\\"\"/) {
    $scnt++;
    if ($scnt == 1) {
      $slstr = "starts/endsWith line\#" . $linecnt;
    } else {
      $slstr = $slstr . "," . $linecnt;
    }
    print "=> $line\n" if ($verbose);
    next;
  }
}

if (!$cnt && !$scnt) {
  print "okay\n" if (!$quiet);
  exit 0;
} else {
  print "$lstr ($cnt)\n" if (!$quiet && $cnt);
  print "$slstr ($scnt)\n" if (!$quiet && $scnt);
  exit $cnt+$scnt;
}

sub Help {
  print "Check for single char QStrings better written as a QChar\n";
  exit 0 if $help;
}

sub Version {
  print "$Prog, version $Version\n";
  exit 0 if $version;
}

sub Explain {
  print "Adding single characters to a QString is faster if the characters are QChars and not QStrings.  For example: QString path = oldpath + \"/\" + base is better written as QString path = oldpath + \'/\' + base. Same holds for arguments to QString::startsWith() and QString::endsWith()\n";
  exit 0 if $explain;
}
