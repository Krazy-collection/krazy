#!/usr/bin/perl -w

eval 'exec /usr/bin/perl -w -S $0 ${1+"$@"}'
    if 0; # not running under some shell
###############################################################################
# Sanity check plugin for the Krazy project.                                  #
# Copyright (C) 2008 by Allen Winter <winter@kde.org>                         #
#                                                                             #
# This program is free software; you can redistribute it and/or modify        #
# it under the terms of the GNU General Public License as published by        #
# the Free Software Foundation; either version 2 of the License, or           #
# (at your option) any later version.                                         #
#                                                                             #
# This program is distributed in the hope that it will be useful,             #
# but WITHOUT ANY WARRANTY; without even the implied warranty of              #
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the                #
# GNU General Public License for more details.                                #
#                                                                             #
# You should have received a copy of the GNU General Public License along     #
# with this program; if not, write to the Free Software Foundation, Inc.,     #
# 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.               #
#                                                                             #
###############################################################################

# Tests KDE source for Qt methods to be avoided.

# Program options:
#   --help:          print one-line help message and exit
#   --version:       print one-line version information and exit
#   --explain:       print an explanation with solving instructions, then exit
#   --installed      file is to be installed
#   --quiet:         suppress all output messages
#   --verbose:       print the offending content

# Exits with status=0 if test condition is not present in the source;
# else exits with the number of failures encountered.

use strict;
use Getopt::Long;
use FindBin qw($Bin);
use lib "$Bin/../../../../lib";
use Krazy::PreProcess;

my($Prog) = "qmethods";
my($Version) = "1.0";

my($help) = '';
my($version) = '';
my($explain) = '';
my($installed) = '';
my($quiet) = '';
my($verbose) = '';

exit 1
if (!GetOptions('help' => \$help, 'version' => \$version,
                'explain' => \$explain, 'installed' => \$installed,
                'verbose' => \$verbose, 'quiet' => \$quiet));

&Help() if $help;
&Version() if $version;
&Explain() if $explain;
if ($#ARGV != 0){ &Help(); exit 0; }

my($f) = $ARGV[0];

# open file and slurp it in (C++, non-headers only)
if ($f =~ m/\.cpp$/ || $f =~ m/\.cxx$/ || $f =~ m/\.cc$/) {
  open(F, "$f") || die "Couldn't open $f";
} else {
  print "okay\n" if (!$quiet);
  exit 0;
}
my(@data_lines) = <F>;
close(F);

# Remove C-style comments and #if 0 blocks from the file input
my(@lines) = RemoveIfZeroBlockC( RemoveCommentsC( @data_lines ) );

my($cnt) = 0;
my($linecnt) = 0;
my($lstr) = "";

my($line);
while ($linecnt < $#lines) {
  $line = $lines[$linecnt++];
  if ($line =~ m+//.*[Kk]razy:excludeall=.*$Prog+ ||
      $line =~ m+//.*[Kk]razy:skip+) {
    $cnt = 0;
    last;
  }
  next if ($line =~ m+//.*[Kk]razy:exclude=.*$Prog+);
  $line =~ s+//.*++;  #skip C++ comments

  &doIt($line,'QWidget::showFullScreen','showFullScreen\s*\(\s*\)');
  &doIt($line,'QWidget::showMaximized','showMaximized\s*\(\s*\)');
  &doIt($line,'QWidget::showMinimized','showMinimized\s*\(\s*\)');
  &doIt($line,'QWidget::showNormal','showNormal\s*\(\s*\)');
  &doIt($line,'QSystemTrayIcon::showMessage','showMessage\s*\(');
}

if (!$cnt) {
  print "okay\n" if (!$quiet);
  exit 0;
} else {
  $lstr =~ s/,$//;
  print "$lstr ($cnt)\n" if (!$quiet);
  exit $cnt;
}

sub doIt() {
  my($line,$qname,$regexp) = @_;
  if (&qMethod($line,$regexp)) {
    $cnt++;
    if ($cnt == 1) {
      $lstr = "line\#" . $linecnt;
    } else {
      $lstr = $lstr . "," . $linecnt;
    }
    $lstr .= " $qname";
    print "=> $line\n" if ($verbose);
  }
}

sub qMethod() {
  my($l,$regexp) = @_;
  if ($l =~ m/[:\.>\s]$regexp/ &&
      $l !~ m/SIGNAL/ && $l !~ m/SLOT/) {
    return 1;
  } else {
    return 0;
  }
}

sub Help {
  print "Check for Qt classes that should be avoided\n";
  exit 0 if $help;
}

sub Version {
  print "$Prog, version $Version\n";
  exit 0 if $version;
}

sub Explain {
  print "A few Qt methods should be avoided because they don't work as expected or because you need to really be an expert to use them correctly. More info will be coming in a Policy document.\n";
  exit 0 if $explain;
}
